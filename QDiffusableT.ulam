local typedef EventWindow.SiteNum SiteNum;

quark QDiffusableT(IDiffusable.Diffusability cDIFFUSABILITY, WindowServices.Radius cDIFFUSION_RADIUS): IDiffusable {

  // constant Diffusability cCOMPLETE_DIFFUSABILITY = 10000u;
  constant Diffusability cCOMPLETE_DIFFUSABILITY = 1000u;

  @Override Diffusability getDiffusability() {
    return cDIFFUSABILITY;
  }

  @Override WindowServices.Radius getDiffusionRadius() {
    return cDIFFUSION_RADIUS;
  }

  // NOTE: should only be called on an atom at the event window origin
  Void diffuse() {
    if (getDiffusability() == 0u || getDiffusionRadius() == 0u)
      return;

    EventWindow ew;
    Random random;

    // Get random site
    SiteNum site = (ew[0] is IBondable)
      ? getBondableDiffusionSite()
      : getDiffusionSite();
    if (site == SiteNum.maxof)
      return;

    // Swap with some probability
    Diffusability d = 0u;
    if (ew.isEmpty(site)) {
      d = getDiffusability();
    } else {
      Atom& other = ew[site];
      if (other as IDiffusable) {
        if (other.getDiffusability() > 0u
            && other.getDiffusionRadius() <= ew.getCoord(site).length())
        {
          Diffusability d1 = getDiffusability();
          Diffusability d2 = other.getDiffusability();
          d = (d1 < d2) ? d1 : d2;
        }
      }
    }
    if (d > 0u && random.oddsOf(d, cCOMPLETE_DIFFUSABILITY)) {
      SwapHelper sh;
      sh.swap(0, site);
    }
  }

  SiteNum getDiffusionSite() {
    WindowServices ws;
    ws.reset(1u, getDiffusionRadius());
    ws.scan(WindowServices.cALL_SITES_HIT);
    return ws.getPick();
  }

  SiteNum getBondableDiffusionSite() {
    EventWindow ew;

    // Collect coordinates of attached non-diffusable atoms

    IBondable& bondable = (IBondable&) ew[0];

    C2D staticCoords[QBond.Index.maxof];
    Unsigned staticCoordNum = 0u;

    for (QBond.Index i = 0u; i < bondable.getBondNum(); i++) {
      QBond& bond = bondable.getBond(i);
      C2D coord = bond.getCoord();
      if (!ew.isEmpty(coord) && !(ew[coord] is IDiffusable))
        staticCoords[staticCoordNum++] = coord;
    }

    if (staticCoordNum == 0u)
      return getDiffusionSite();

    // Make sites near collected coordinates more attractive
    // TODO: try different parameters

    WindowServices ws;
    ws.reset(1u, getDiffusionRadius());
    for (Int s = ws.next(); s >= 0; s = ws.next()) {
      SiteNum site = (SiteNum) s;
      C2D coord = ew.getCoordRaw(site);

      Unsigned weight = 1u;
      for (Unsigned i = 0u; i < staticCoordNum; i++) {
        Unsigned dist = coord.manhattanDistance(staticCoords[i]);
        if (0u < dist && dist <= 2u) {
          weight = 2u;
          break;
        }
      }
      ws.hit(0u, weight);
    }
    return ws.getPick();
  }
}
